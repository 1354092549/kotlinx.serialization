/*
 *  Copyright 2018 JetBrains s.r.o.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'

kotlin {
    jvm('jvm') {
        configure([compilations.main, compilations.test]) {
            kotlinOptions {
                jvmTarget = '1.6'
            }
        }
    }

    sourceSets {
        jvmMain {
            kotlin.srcDirs = ['src/main/kotlin']
        }
        jvmTest {
            kotlin.srcDirs = ['src/test']
        }
    }

    if (rootProject.ext.jvm_ir_target_enabled) {
        jvm('jvm') {
            attributes.attribute(rootProject.ext.jvmCompilerTypeAttr, 'jvm')
        }
        jvm('jvmIr') {
            attributes.attribute(rootProject.ext.jvmCompilerTypeAttr, 'jvmIr')
            compilations.all {
                kotlinOptions {
                    jvmTarget = '1.6'
                    useIR = true
                }
            }
        }

        sourceSets {
            jvmIrMain {
                dependsOn jvmMain
            }
            jvmIrTest {
                dependsOn jvmTest
            }
        }
    }
}

dependencies {
    jvmMainImplementation project(':kotlinx-serialization-core')
    jvmMainApi 'org.jetbrains.kotlin:kotlin-stdlib'

    jvmMainApi 'com.typesafe:config:1.3.2'

    jvmTestImplementation "org.jetbrains.kotlin:kotlin-test"
    jvmTestImplementation group: 'junit', name: 'junit', version: '4.12'
}

apply from: rootProject.file('gradle/publishing.gradle')